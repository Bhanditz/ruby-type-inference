buildscript {
    repositories {
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.2.17"
}

repositories {
    maven { url "https://www.jetbrains.com/intellij-repository/snapshots" }
}

dependencies {
    def withoutKotlinAndMySql = {
        exclude group: 'org.jetbrains.kotlin'
        exclude group: 'mysql'
    }
    def withoutSlfAndKotlinAndMySql = {
        exclude group: 'org.slf4j'//, module: 'slf4j-api'
        exclude group: 'org.jetbrains.kotlin'
        exclude group: 'mysql'
    }

    compile project(':ruby-call-signature'), withoutKotlinAndMySql
    compile project(':storage-server-api'), withoutSlfAndKotlinAndMySql
    compile project(':contract-creator'), withoutSlfAndKotlinAndMySql
    compile project(':state-tracker'), withoutSlfAndKotlinAndMySql

    // https://mvnrepository.com/artifact/com.h2database/h2
    compile group: 'com.h2database', name: 'h2', version: '1.4.196'

    compile group: 'com.jetbrains.intellij.idea', name: 'ruby-test-framework', version: ideaVersion

}

sourceSets {
    main.java.srcDirs = ['src']
    main.resources.srcDirs = ['resources']
    test.java.srcDirs = ['src/test/java']
}

intellij {
    version "IU-$ideaVersion"
    pluginName 'ruby-runtime-stats'
    plugins = ["org.jetbrains.plugins.ruby:$rubyPluginVersion"]
}

patchPluginXml{
    sinceBuild '173.3622'
    untilBuild '181.*'
}

prepareSandbox.doLast {
    def destDir = "$it.destinationDir/$intellij.pluginName"
    copy {
        from sourceSets.main.resources.include("**/*.rb", "**/*.db")
        into destDir
    }
}